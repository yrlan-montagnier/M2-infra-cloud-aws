# TP7 - Exercice 2
## Contexte
Maintenant que l'application Nextcloud est d√©ploy√©e et accessible via l'ALB, l'√©quipe de direction souhaite am√©liorer la r√©silience de l'infrastructure. 

L'objectif est de pouvoir g√©rer la reprise d'activit√© en cas de panne d'une AZ ou d'une instance EC2 et √©galement de pr√©parer le terrain pour une gestion automatique de la capacit√© en fonction de la charge.

On vous demande de travailler conjointement avec l'√©quipe DevOps afin de mettre en place un Auto Scaling Group (ASG) pour g√©rer dynamiquement les instances Nextcloud. Cette solution devra permettre de relancer automatiquement une instance EC2 dans une autre AZ en cas de panne.


## Objectifs
* Suivre les instructions de l'√©quipe DevOps pour mettre en place un Auto Scaling Group pour les instances Nextcloud.
* Tester le d√©ploiement initial de l'Auto Scaling Group sans scaling policy.
* Simuler une panne d'AZ et v√©rifier que l'application est automatiquement red√©ploy√©e dans une autre AZ sans perte de donn√©es.

## Etapes de r√©alisation
### üìå 1. Cr√©ation d‚Äôune AMI de l‚Äôinstance Nextcloud existante

#### Consignes
* Cr√©er une AMI √† partir de l'instance Nextcloud existante une fois que l'application est configur√©e et fonctionnelle.
* Le processus de cr√©ation automatique de l'AMI sera r√©alis√© par l'√©quipe DevOPS dans un second temps.
* Pour cette √©tape, vous pouvez cr√©er l'AMI manuellement dans la console AWS.
* Assurez-vous que l'AMI soit nomm√©e de mani√®re explicite pour faciliter son identification `<username>-<tp_directory>-nextcloud-<date>`.
* Assurez-vous que l'AMI soit correctement tagg√©e (tags `Name` et `Owner`).

---

#### Mise en place
1. Acc√©der √† la **console AWS** ‚Üí **EC2** ‚Üí **Instances**.
2. S√©lectionner l'instance Nextcloud et cliquer sur **"Cr√©er une image"**.
3. Donner un nom explicite, par ex. :  
   **`ymontagnier-ami-nextcloud`**
4. V√©rifier que l‚ÄôAMI est bien cr√©√©e dans **Images > AMI**
5. Ajouter les tags :
   - `Name = ymontagnier-ami-nextcloud`
   - `Owner = ymontagnier`

### üìå 2. R√©cup√©ration de l‚ÄôID de l‚ÄôAMI en Terraform
#### Consignes
* Cr√©er la configuration Terraform pour r√©cup√©rer dynamiquement l'ID de l'AMI cr√©√©e pr√©c√©demment.
* Cette configuration devra r√©cup√©rer l'ID de l'AMI la plus r√©cente qui correspond aux crit√®res de rechercher suivants :
* Le nom de l'AMI doit commencer par `<username>-ami-nextcloud`.
* L'AMI doit √™tre dans un √©tat disponible (available).
* L'AMI doit avoir comme propri√©taire le compte AWS actuel (self).

#### Mise en place
Ajout du bloc Terraform pour r√©cup√©rer dynamiquement l‚ÄôAMI la plus r√©cente :
:file_folder: `instances-ami.tf`
```hcl
# Ce fichier contient la d√©finition de la ressource aws_ami qui permet de r√©cup√©rer l'AMI la plus r√©cente correspondant √† un pattern donn√©
# Cette ressource est d√©finie dans un bloc data
data "aws_ami" "nextcloud" {
  most_recent = true
  owners      = ["self"]

  # Filtre pour r√©cup√©rer l'AMI la plus r√©cente correspondant au pattern "ymontagnier-*-nextcloud-*"
  filter {
    name   = "name"
    values = ["${local.user}-ami-nextcloud*"]
  }

  # Filtre pour r√©cup√©rer une AMI disponible
  filter {
    name   = "state"
    values = ["available"]
  }
}

output "ami_id" {
  value = data.aws_ami.nextcloud.id
}

output "ami_name" {
  value = data.aws_ami.nextcloud.name
}
```
### üìå 3. Cr√©ation du Launch Template
#### Consigne
* Cr√©er la configuration Terraform qui d√©ploie un Launch Template.
    * Utiliser l'AMI cr√©√©e pr√©c√©demment.
    * Configurer les param√®tres des instances de mani√®re identique √† ce qui avait √©t√© fait pour l'instance EC2 nextcloud (type, key, security group) √† l'exception du userdata qui ne servira plus.
    * Ne pas oublier de configurer les tags pour :
        * le launch Template
        * les instances
        * les volumes
        * les interfaces r√©seau

#### Mise en place
Un Launch Template d√©finit comment les instances seront cr√©√©es dans l‚ÄôASG.
:file_folder: `instances-asg.tf`

```hcl
# Template pour la cr√©ation d'un Auto Scaling Group (ASG) avec Terraform
# Cette ressource d√©finit une ressource aws_launch_template, ce qui permet de cr√©er un Launch Template
resource "aws_launch_template" "nextcloud" {
  name_prefix            = "${local.name}-nextcloud-lt"
  image_id               = data.aws_ami.nextcloud.id
  instance_type          = "t3.micro"
  key_name               = aws_key_pair.nextcloud.key_name
  vpc_security_group_ids = [aws_security_group.nextcloud_sg.id]

  tag_specifications {
    resource_type = "instance"
    tags = {
      Name  = "${local.name}-nextcloud-instance"
      Owner = local.user
    }
  }

  tag_specifications {
    resource_type = "volume"
    tags = {
      Name  = "${local.name}-nextcloud-volume"
      Owner = local.user
    }
  }
}
```
### üìå 4. Cr√©ation de l‚ÄôAuto Scaling Group
#### Consigne
* Cr√©er la configuration Terraform qui d√©ploie un Auto Scaling Group nomm√© `<username>-<tp_directory>-nextcloud`.
* Cette premi√®re configuration ne g√©rera pas la capacit√© automatiquement, elle sera utilis√©e pour tester le d√©ploiement initial de l'ASG :
    * Utiliser le Launch Template cr√©√© pr√©c√©demment dans sa version la plus r√©cente.
    * Configurer le nombre d'instances minimum, d√©sir√© et maximum √† 1.
    * Configurer le type de health check sur le Load Balancer.
    * Configurer les sous-r√©seaux o√π les instances EC2 seront d√©ploy√©es de sorte √† ce qu'elles soient r√©parties sur les 3 AZ.
    * Configurer le rattachement automatique des instances au target group utilis√© par l'ALB.
    * Configurer les tags pour l'ASG comme ceci :-1: 
    ```
    tag {
      key                 = "Owner"
      value               = local.user
      propagate_at_launch = false
    }
    ```

#### Mise en place
:file_folder: `instances-asg.tf`
L‚ÄôASG d√©ploiera une instance et la relancera en cas de panne.

```hcl
# Cette ressource d√©finit un Auto Scaling Group (ASG) qui utilise le Launch Template pr√©c√©demment cr√©√© dans sa version la plus r√©cente
resource "aws_autoscaling_group" "nextcloud" {
  name                = "${local.name}-nextcloud-asg"
  desired_capacity    = 1                                    # Nombre d'instances souhait√©
  min_size            = 1                                    # Nombre minimum d'instances
  max_size            = 1                                    # Nombre maximum d'instances
  vpc_zone_identifier = [for s in aws_subnet.private : s.id] # Subnets priv√©s

  # Utiliser le Launch Template pour cr√©er les instances
  launch_template {
    id      = aws_launch_template.nextcloud.id
    version = "$Latest"
  }

  health_check_type         = "ELB"                               # Utiliser le Load Balancer pour la v√©rification de l'√©tat de sant√© des instances
  health_check_grace_period = 300                                # D√©lai entre le d√©marrage de l'instance et le d√©but des v√©rifications de l'√©tat de sant√©
  target_group_arns         = [aws_lb_target_group.nextcloud.arn] # Attacher les instances au Target Group du Load Balancer

  # D√©finition des tags pour l'Auto Scaling Group
  tag {
    key                 = "Owner"
    value               = local.user
    propagate_at_launch = false
  }

  tag {
    key                 = "Name"
    value               = "${local.name}-nextcloud-asg"
    propagate_at_launch = false
  }
}
```

Ajout d'output pour r√©cup√©rer l'IP priv√©e des instances Nextcloud
```
data "aws_instances" "nextcloud_asg" {
  filter {
    name   = "tag:aws:autoscaling:groupName"
    values = [aws_autoscaling_group.nextcloud.name]
  }
}

output "asg_instances_nextcloud_private_ips" {
  value = data.aws_instances.nextcloud_asg.private_ips
}
```

### üìå 5. Suppression de l‚Äôancienne instance Nextcloud
Commenter la ressource Terraform existante dans le fichier ec2.tf :

```
# resource "aws_instance" "nextcloud" {
#   ami                    = "ami-09a9858973b288bdd"
#   instance_type          = "t3.micro"
#   subnet_id              = aws_subnet.private["b"].id           # Changer cette ligne pour changer l'AZ
#   key_name               = aws_key_pair.nextcloud.key_name      # Utiliser la paire de cl√©s nextcloud
#   vpc_security_group_ids = [aws_security_group.nextcloud_sg.id] # Utiliser le groupe de s√©curit√© nextcloud_sg
#   user_data              = local.nextcloud_userdata             # Utiliser le script de d√©marrage g√©n√©r√© dans locals.tf

#   # user_data = templatefile("setup_efs.sh", {             # Utiliser un script de d√©marrage pour monter le syst√®me de fichiers EFS
#   #   efs_dns = aws_efs_file_system.nextcloud_efs.dns_name # Passer le nom DNS du syst√®me de fichiers EFS au script de d√©marrage
#   # })

#   depends_on = [aws_nat_gateway.public_nat, aws_route_table_association.private] # Attendre que la gateway NAT et la route vers internet soient cr√©√©es

#   tags = {
#     Name = "${local.name}-nextcloud"
#   }
# }
```

### üìå6. D√©ploiement de l'Auto Scaling Group

Puis appliquer les changements :
```
terraform apply
```
> ‚ö†Ô∏è L‚Äôancienne instance sera supprim√©e, et une nouvelle sera lanc√©e via l‚ÄôASG.

## ‚úÖ Test de la r√©silience
1. Connexion √† Nextcloud et v√©rification de l'acc√®s.
2. Cr√©ation d‚Äôun fichier dans Nextcloud.
3. Simuler une panne :
    * AWS Console ‚Üí EC2 ‚Üí s√©lection de l'instance Nextcloud ‚Üí Actions ‚Üí Instance State ‚Üí Terminate.

4. Attente quelques minutes, puis v√©rification :
* Une nouvelle instance est cr√©√©e dans une autre AZ ? ‚úÖ
* L'application Nextcloud reste accessible via l‚ÄôALB? ‚úÖ
* Le fichier pr√©c√©demment cr√©√© est toujours l√† (test de persistance des donn√©es)? ‚úÖ


## Conclusion

### ‚úÖ Objectifs atteints
‚úî Auto Scaling Group op√©rationnel
‚úî Gestion automatique des pannes
‚úî Test de persistance des donn√©es r√©ussi
